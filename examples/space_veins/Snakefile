import os
import glob
import json
import sys
sys.path.append(os.path.join(sys.path[0],"..","..","scripts", "plots"))
from satname_to_modname import satname_to_modname
sys.path.append(os.path.join(sys.path[0],"..","..",".conda", "lib", "python3.10", "site-packages"))
sys.path.append(os.path.join(sys.path[0],"..","..","scripts", "keplertraces"))
from tleparse import read
from parseomnetini import parseomnetini
from pathlib import Path
import configparser

configfile: "smk.config.yaml"

def get_tle_path(constellation):
    for fname in os.listdir("./tles/"):
        if constellation in fname:
            return "tles/" + fname

def get_tles_paths():
    tles_fname_prefixes = config["tles_prefixes"]
    tles_fpaths = [] 
    
    for fname in os.listdir("./tles/"):
        if fname.split("_")[0] in tles_fname_prefixes and fname.endswith(".txt"):
            tles_fpaths.append(f'{"tles/" + fname}')
    
    return tles_fpaths

def quote_strs(tles_paths: list[str]):
    return [str('"' + tle_path + '"') for tle_path in tles_paths]

def get_traces_dir(itrf=None):
    base_dir = config["traces_dir"].removesuffix("/")
    orekit_str = "_orekit" if config["use_orekit"] else ""
    itrf_str = "_itrf" if itrf else ""

    return base_dir + itrf_str + orekit_str + "/"

rule calc_tle_times:
    input:
        script="../../scripts/keplertraces/tletimes.py",
        tles=lambda wildcards: get_tle_path(wildcards.constellation)
    output:
        "tles/{constellation}_times.json"
    shell:
        "python3 {input.script} {input.tles}"

rule update_omnetini:
    input:
        "../../scripts/keplertraces/updateomnetini.py",
        ini_template="omnetpp.ini.template",
        tles_paths=get_tles_paths()
    params:
        # as snakemake won't accept filepath strings containing quotes as input
        # but updateomnetini.py requires them
        quoted_tles_paths= lambda wildcards, input: quote_strs(input.tles_paths),
        traces_dir = get_traces_dir(True)
    output:
        "omnetpp.ini"
    shell:
        """python3 ../../scripts/keplertraces/updateomnetini.py {input.ini_template} {params.traces_dir} {params.quoted_tles_paths}
        
        cp backups/omnetpp.ini old/omnetpp.ini

        cp omnetpp.ini backups/omnetpp.ini
        """

def config_params_changed(config: str) -> bool:
    inipath = "omnetpp.ini"
    old_inipath = "old/omnetpp.ini"
    
    params_tuple = parseomnetini(inipath, config)
    old_params_tuple = parseomnetini(old_inipath, config)

    for val_i in range(0, len(params_tuple)):
        if params_tuple[val_i] != old_params_tuple[val_i]:
            return True

    return False    

rule get_omnetini_traceparams:
    input:
        "../../scripts/keplertraces/parseomnetini.py",
        ini="omnetpp.ini"
    output:
        #get_traces_dir(True) + "params/{opp_config}_update.done"
        get_traces_dir(True) + "params/{opp_config}_params.txt"
    params:
        # gets updated by script if values actually change"
    shell:
        # only touch output, so that only updates of params matter for rule create_traces
        """
        python3 ../../scripts/keplertraces/parseomnetini.py {input.ini} {wildcards.opp_config} {output}
        """

rule create_traces:
    input:
        "../../scripts/keplertraces/create_traces.py",
        params_path=lambda wildcards: get_traces_dir(True) + "params/" + get_debug_str() + "{constellation}-kepler_params.txt",
        tles_path=lambda wildcards: glob.glob("tles/" + wildcards.constellation + "_*.txt")
    params:
        configname=lambda wildcards: expand("{optional_Debug}{constellation}-kepler", 
                                        optional_Debug="Debug-" if config["debug_mode"] else "",
                                        constellation=wildcards.constellation),
        itrf_option= lambda wildcards: "-i" if "itrf" in wildcards.traces_dir else "",
        orekit_option="-o" if config["use_orekit"] else ""
    output:
        "{traces_dir}/{constellation}_traces.done"
    shell:
        """
        python3 ../../scripts/keplertraces/create_traces.py {input.params_path} {input.tles_path} {wildcards.traces_dir} -c {params.configname} {params.itrf_option} {params.orekit_option}
        date +%Y%m%d%H%M%S > {output}
        """

rule make_makefiles:
    input:
        "../../Makefile"
    output:
        "../../src/Makefile"
    shell:
        "cd ../../ && make makefiles"

rule build_space_veins_devcontainer:
    input:
        "../../Makefile",
        "cleanall.done"
    output:
        "../../bin/space_veins_run",
        temp("build_devcontainer.done")
    #container:
    #    "../../singularity/singularity-space_veins.sif"
    threads:
        workflow.cores
    shell:
        """
        cd ../../
        make makefiles
        bear --append -- make all -j{threads}
        cd examples/space_veins
        touch "build_devcontainer.done"
        """

rule build_space_veins_singularity:
    input:
        "../../Makefile",
        "cleanall.done"
    output:
        "../../bin/space_veins_run",
        temp("build_singularity.done")
    container:
        "../../singularity/singularity-space_veins.sif"
    threads:
        workflow.cores
    shell:
        """
        make makefiles
        ucc make -j{threads} all
        make bin/space_veins_run
        touch "cleanall.done"
        """

rule cleanSpaceVeins:
    input:
        "../../Makefile",
        "../../src/Makefile"
    output:
        temp("cleanall.done")
    #container:
    #    "../../singularity/singularity-space_veins.sif"
    threads:
        1
    shell:
        """
        cd ../../ &&
        make cleanall &&
        cd examples/space_veins &&
        touch "cleanall.done" 
        """

rule createRunmakerLogFile:
    output:
        "runs/{oppConfig}_runs.log"
    shell:
        "touch {output}"

def get_trace_requirement(wildcards):
    if "kepler" in wildcards.oppConfig:
        return get_traces_dir(True) + get_const_from_config(wildcards.oppConfig) + "_traces.done"
    else: 
        return []

rule createRunFile:
    input:
        # only require trace files for Kepler-orbit-based runs
        # either traces/ or traces_orekit/ with use_orekit:true in config
        # ---
        # placed here instead of in rule 'run' so that run file is regenerated with more recent traces
        # otherwise, run file still has run marked as 'd', done
        get_trace_requirement,
        "omnetpp.ini"
    output:
        "runs/{oppConfig}_runs.txt"
    # container:
    #    "../../singularity/singularity-space_veins.sif"
    # params:
    #    shell="echo '. ./run -u Cmdenv -c {wildcards.oppConfig} -r 1' > {output}"
    shell:
        """
            echo '. ./run -u Cmdenv -c {wildcards.oppConfig} -r 1' > {output}
        """

rule createRunmakerFiles:
    input:
        expand("runs/{optional_Debug}{constellation}-{mobility}_runs.log", 
                optional_Debug="Debug-" if config["debug_mode"] else "" ,
                constellation=config["tles_prefixes"],
                mobility=config["mobilities"]),
        expand("runs/{optional_Debug}{constellation}-{mobility}_runs.txt", 
                optional_Debug="Debug-" if config["debug_mode"] else "" ,
                constellation=config["tles_prefixes"],
                mobility=config["mobilities"]),
    output:
        temp("runmaker.done")
    shell:
        "touch runmaker.done"

def get_const_from_config(config_str):
    config = config_str.replace("Debug-","")
    constellation = config.split("-")[0]
    return constellation

rule run:
    input:
        logFile="runs/{oppConfig}_runs.log",
        runFile="runs/{oppConfig}_runs.txt"
    output:
        "runs/{oppConfig}_done.txt"
    threads:
        1
    #container:
    #    "../../singularity/singularity-space_veins.sif"
    shell:
        """
        python3 ../../lib/runmaker/runmaker4.py --jobs={threads} -l {input.logFile} --loglines=200 {input.runFile} 
        date +%Y%m%d%H%M%S > {output}
        """

rule runAll:
    input:
        expand("runs/{optional_Debug}{constellation}-{mobility}_done.txt", 
                optional_Debug="Debug-" if config["debug_mode"] else "" ,
                constellation=config["tles_prefixes"],
                mobility=config["mobilities"])
    output:
        "runs/runall.done"
    shell:
        "touch runs/runall.done"

def format_coord_vectors(wildcards):
    if (wildcards.coordframe == "wgs84"):
        return ("--list -F " + "wgs84CoordLat:vector=wgs84CoordLat_vector" +
                                " -F wgs84CoordLon:vector=wgs84CoordLon_vector " +
                                " -F wgs84CoordAlt:vector=wgs84CoordAlt_vector")
    elif (wildcards.coordframe == "itrf"):
        return ("--list -F " + "itrfCoordX:vector=itrfCoordX_vector" +
                                " -F itrfCoordY:vector=itrfCoordY_vector" +
                                " -F itrfCoordZ:vector=itrfCoordZ_vector")
    elif (wildcards.coordframe == "omnet"):
        return ("--list -F " + "omnetCoordX:vector=omnetCoordX_vector" +
                                " -F omnetCoordY:vector=omnetCoordY_vector" +
                                " -F omnetCoordZ:vector=omnetCoordZ_vector")
    else:
        raise ValueError(f"coordinate frame {wildcards.coordframe} is not supported for csv results")

def formatItervars(wildcards):
    i = ""
    for s in config["itervars"]:
        i += "-I " + s + " "
    return i

def get_orekit_path_option():
    if config["use_orekit"]:
        return "orekit/"
    else:
        return "no-orekit/"

rule vec2csv:
    input:
        "runs/{oppConfig}_done.txt",
        "../../scripts/time_sort_csv.py"
    output:
        "csv/vectors/" + get_orekit_path_option() + "{oppConfig}_{coordframe}_sorted.csv"
    params:
        itervars=formatItervars,
        vectors=format_coord_vectors,
        moduleRegex='^SatelliteExampleScenario\.leo',
        unsorted_path="csv/vectors/" + get_orekit_path_option() + "{oppConfig}_{coordframe}.csv"
    threads:
        1
    shell:
        """
        perl ../../lib/veins_scripts/eval/opp_vec2csv.pl -v -m emt -M {params.moduleRegex} {params.vectors} -A repetition {params.itervars} results/{wildcards.oppConfig}*.vec > {params.unsorted_path}
        python3 ../../scripts/time_sort_csv.py {params.unsorted_path}  
        rm {params.unsorted_path}
        """

def get_const_sat_mods(constellation: str):

    const_tles_fname = filter( lambda tles_path: tles_path.startswith(constellation), os.listdir("./tles/")).__next__()
    tles = read("./tles/" + const_tles_fname)

    sat_mods = set()
    for tle in tles:
        sat_mods.add( satname_to_modname(tle.name) )
    
    return sat_mods

def get_debug_str():
    return "Debug-" if config["debug_mode"] else ""

def format_constellation_satvec_csvnames(wildcards):
    config_name = get_debug_str() + wildcards.constellation + "-" + wildcards.mobility 

    files = []

    for frame in config["coord_frames"]:
        #currently, use all frames, as poliastro itrf traces are used as simulation input
        #if frame == "itrf" and wildcards.mobility == "kepler":
        #    continue
        f = "csv/vectors/" + get_orekit_path_option() + config_name + "_" + frame + "_sorted.csv"
        files.append(f)
    
    return files

rule sat_vec_to_csv_constellation:
    input:
        "runs/" + get_debug_str() + "{constellation}-{mobility}_done.txt",
        format_constellation_satvec_csvnames
    output:
        "csv/" + get_orekit_path_option() + get_debug_str() + "{constellation}-{mobility}_vecs.done"
    shell:
        "touch {output}"

def get_coord_inputs(wildcards):
    # code below only use for wgs84 traces as simulation inputs
    
    #if (wildcards.mobility1 == "kepler" or wildcards.mobility2 == "kepler") and wildcards.coord_frame == "itrf":
    #    other_mobility = wildcards.mobility1 if wildcards.mobility2 == "kepler" else wildcards.mobility2
    #    return [get_traces_dir(True) + "{constellation}_traces.done",
    #            "csv/vectors/{constellation}-" + other_mobility + "_{coord_frame}_sorted.csv"]
    
    #else:
        return ["csv/vectors/" + get_orekit_path_option() + "{constellation}-{mobility1}_{coord_frame}_sorted.csv",
                "csv/vectors/" + get_orekit_path_option() + "{constellation}-{mobility2}_{coord_frame}_sorted.csv"]

def get_traces_dir_from_donefile(donefile_path):
    donefile_name = donefile_path.split("/")[-1]
    constellation = donefile_name.split("_")[0]
    all_traces_dir = donefile_path.removesuffix(donefile_name)
    for file_or_dir_name in os.listdir(all_traces_dir):
        if file_or_dir_name.startswith(constellation) and not file_or_dir_name.endswith("_traces.done"):
            return all_traces_dir + file_or_dir_name
    error_str = f"No directory for constellation {constellation} was found!"
    raise NameError(error_str)

rule calc_pos_diffs:
    input:
        "../../scripts/plots/positional_differences.py",
        trace_done_or_csvs=get_coord_inputs
    output:
        "csv/pos_diff/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv"
    params:
        traces_dir_or_csv = lambda wildcards, input: input.trace_done_or_csvs[0] if input.trace_done_or_csvs[0].endswith(".csv") else get_traces_dir_from_donefile(input.trace_done_or_csvs[0]),
    shell:
        "python3 ../../scripts/plots/positional_differences.py {params.traces_dir_or_csv} {input.trace_done_or_csvs[1]} {wildcards.coord_frame} -c > {output}"

rule plot_pos_diffs:
    input:
        "../../scripts/plots/plot_differences.py",
        in_csv="csv/pos_diff/"+ get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv"
    output:
        "plots/pos_diff/" + get_orekit_path_option() + "{constellation}/"+ get_debug_str() + "{coord_frame}_{mobility1}-{mobility2}_{leo_modname}_distances.png"
    shell:
        "python3 ../../scripts/plots/plot_differences.py {input.in_csv} '{wildcards.leo_modname}' '{output}'"

rule plot_pos_diffs_ecdf:
    input:
        "../../scripts/plots/plot_differences_ecdf.py",
        in_csv="csv/pos_diff/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv"
    output:
        "plots/pos_diff/"+ get_orekit_path_option() +"{constellation}/"+ get_debug_str() + "{coord_frame}_{mobility1}-{mobility2}_{leo_modname}_distances_ecdf.png"
    shell:
        "python3 ../../scripts/plots/plot_differences_ecdf.py {input.in_csv} '{wildcards.leo_modname}' '{output}'"

rule plot_pos_diffs_both:
    input:
        "../../scripts/plots/plot_differences_both.py",
        tle_times="tles/{constellation}_times.json",
        in_csv="csv/pos_diff/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv"
    output:
        "plots/pos_diff/" + get_orekit_path_option() + "{constellation}/"+ get_debug_str() + "{coord_frame}_{mobility1}-{mobility2}_{leo_modname}_distances_both.png"
    shell:
        "python3 ../../scripts/plots/plot_differences_both.py {input.in_csv} '{wildcards.leo_modname}' {input.tle_times} '{output}'"

def get_const_pos_diff_paths(wildcards):
    return expand("plots/pos_diff/"+ get_orekit_path_option()+ "{constellation}/" + get_debug_str() + "{coord_frame}_{mobility1}-{mobility2}_{leo_modname}_distances_both.png",
                constellation=wildcards.constellation, 
                coord_frame=wildcards.coord_frame, 
                mobility1=wildcards.mobility1, 
                mobility2=wildcards.mobility2,
                leo_modname=get_const_sat_mods(wildcards.constellation))

rule plot_const_pos_diffs:
    input:
        get_const_pos_diff_paths
    output:
        "plots/pos_diff/" + get_orekit_path_option() + "{constellation}/" + get_debug_str() + "{coord_frame}_{mobility1}-{mobility2}_distances.done"
    shell:
        "touch {output}"

rule plot_diff_distributions:
    input:
        "../../scripts/plots/plot_difference_distributions.py",
        in_csv="csv/pos_diff/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv"
    output:
        "plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_sum-histogram.png",
        "plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_sum-histogram.html",
        "plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_boxplot.png",
        "plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_boxplot.html"
    params:
        output_dir="plots/pos_diff_distr/" + get_orekit_path_option()
    shell:
        """
        python3 ../../scripts/plots/plot_difference_distributions.py {input.in_csv} {params.output_dir} {wildcards.constellation}_{wildcards.coord_frame}_{wildcards.mobility1}-{wildcards.mobility2}_
        """

rule plot_all_diff_distributions:
    input:
        "../../scripts/plots/plot_difference_distributions.py",
        expand("plots/pos_diff_distr/{orekit}{debug}{constellation}_itrf_sgp4-kepler_sum-histogram.png",
                orekit=get_orekit_path_option(), debug=get_debug_str(), constellation=config["tles_prefixes"]),
        expand("plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_itrf_sgp4-kepler_sum-histogram.html",
                orekit=get_orekit_path_option(), debug=get_debug_str(), constellation=config["tles_prefixes"]),
        expand("plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_itrf_sgp4-kepler_boxplot.png",
                orekit=get_orekit_path_option(), debug=get_debug_str(), constellation=config["tles_prefixes"]),
        expand("plots/pos_diff_distr/" + get_orekit_path_option() + get_debug_str() + "{constellation}_itrf_sgp4-kepler_boxplot.html",
                orekit=get_orekit_path_option(), debug=get_debug_str(), constellation=config["tles_prefixes"])
    output:
        "plots/pos_diff_distr/.done"
    shell:
        "touch {output}"

rule plot_orekit_comparison_histogram:
    input:
        "../../scripts/plots/plot_orekit_comparison_histogram.py",
        no_orekit_csv="csv/pos_diff/no-orekit/" + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv",
        orekit_csv="csv/pos_diff/orekit/" + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_distances.csv"
    output:
        "plots/pos_diff_distr/" + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_orekit-histogram.{format}"
    shell:
        "python3 ../../scripts/plots/plot_orekit_comparison_histogram.py {input.no_orekit_csv} {input.orekit_csv} {output}"

rule plot_all_orekit_comparison_histrograms:
    input:
        "../../scripts/plots/plot_orekit_comparison_histogram.py",
        expand("plots/pos_diff_distr/{debug}{constellation}_itrf_sgp4-kepler_orekit-histogram.{format}", 
                debug=get_debug_str(), 
                constellation=config["tles_prefixes"],
                format=["txt","png"])
    output:
        "plots/pos_diff_distr/histograms.done"
    shell:
        "touch {output}"

rule calc_pos_diff_diffs_itrf_omnet:
    input:
        "csv/pos_diff/" + get_orekit_path_option() + get_debug_str() + "{constellation}_itrf_{mobility1}-{mobility2}_distances.csv",
        "csv/pos_diff/" + get_orekit_path_option() + get_debug_str() + "{constellation}_omnet_{mobility1}-{mobility2}_distances.csv",
        "../../scripts/plots/diff_differences.py"
    output:
        "csv/pos_diff_diffs/" + get_orekit_path_option() + get_debug_str() + "{constellation}_itrf2omnet_{mobility1}-{mobility2}_distances.csv" 
    shell:
        "python3 ../../scripts/plots/diff_differences.py '{input[0]}' '{input[1]}' > {output}"

rule plot_pos_diff_diffs_both:
    input:
        "../../scripts/plots/plot_differences_both.py",
        in_csv="csv/pos_diff_diffs/" + get_orekit_path_option() + get_debug_str() + "{constellation}_itrf2omnet_{mobility1}-{mobility2}_distances.csv"
    output:
        "plots/pos_diff_diffs/" + get_orekit_path_option() + "{constellation}/"+ get_debug_str() + "itrf2omnet_{mobility1}-{mobility2}_{leo_modname}_distances_both.png"
    shell:
        "python3 ../../scripts/plots/plot_differences_both.py {input.in_csv} '{wildcards.leo_modname}' '{output}'"

def get_const_pos_diff_diffs_paths(wildcards):
    return expand("plots/pos_diff_diffs/"+ get_orekit_path_option()+ "{constellation}/" + get_debug_str() + "itrf2omnet_{mobility1}-{mobility2}_{leo_modname}_distances_both.png",
                constellation=wildcards.constellation,  
                mobility1=wildcards.mobility1, 
                mobility2=wildcards.mobility2,
                leo_modname=get_const_sat_mods(wildcards.constellation))

rule plot_const_pos_diff_diffs:
    input:
        get_const_pos_diff_diffs_paths
    output:
        "plots/pos_diff_diffs/" + get_orekit_path_option() + "{constellation}/" + get_debug_str() + "itrf2omnet_{mobility1}-{mobility2}_distances.done"
    shell:
        "touch {output}"

rule calc_omnet_coord_stats:
    input:
        in_csv="csv/vectors/" + get_orekit_path_option() + "{oppConfig}_omnet_sorted.csv",
        script="../../scripts/plots/sop_stats.py"
    output:
        "csv/sop/" + get_orekit_path_option() + "{oppConfig}_distances.csv",
        "csv/sop/" + get_orekit_path_option() + "{oppConfig}_angles.csv",
        "csv/sop/" + get_orekit_path_option() + "{oppConfig}_delays.csv"
    params:
        output_dir="csv/sop/" + get_orekit_path_option()
    shell:
        "python3 {input.script} {input.in_csv} 0 0 0 {params.output_dir} -e 25"

rule plot_omnet_coord_stats:
    input:
        script="../../scripts/plots/plot_sop_stats.py",
        tle_times="tles/{constellation}_times.json",
        angle_csvs=lambda wildcards: expand("csv/sop/" + get_orekit_path_option() + "{constellation}-{mobility}_angles.csv",
                                    constellation=wildcards.constellation,
                                    mobility=["sgp4","kepler"]),
        dist_csvs=lambda wildcards: expand("csv/sop/" + get_orekit_path_option() + "{constellation}-{mobility}_distances.csv",
                                    constellation=wildcards.constellation,
                                    mobility=["sgp4","kepler"]),
        delay_csvs=lambda wildcards: expand("csv/sop/" + get_orekit_path_option() + "{constellation}-{mobility}_delays.csv",
                                    constellation=wildcards.constellation,
                                    mobility=["sgp4","kepler"])
    output:
        "plots/sop/" + get_orekit_path_option() + "{constellation}/sgp4-kepler_{modname}_angles_distances_delays.html",
        "plots/sop/" + get_orekit_path_option() + "{constellation}/sgp4-kepler_{modname}_angles_distances_delays.png"
    params:
        output_base_path = "plots/sop/" + get_orekit_path_option() + "{constellation}/sgp4-kepler_{modname}_angles_distances_delays"
    shell:
        "python3 {input.script} '{wildcards.modname}' --angles_csv_paths {input.angle_csvs} --distances_csv_paths {input.dist_csvs} --delays_csv_paths {input.delay_csvs} --min_angle 25 --tle_times_path {input.tle_times} '{params.output_base_path}' --formats png html"

def get_const_pos_omnet_stats_plot_paths(wildcards):
    return expand("plots/sop/" + get_orekit_path_option() + "{constellation}/sgp4-kepler_{modname}_angles_distances_delays.{format}",
                constellation=wildcards.constellation,  
                modname=get_const_sat_mods(wildcards.constellation),
                format=["png", "html"])

rule plot_const_omnet_coord_stats:
    input:
        get_const_pos_omnet_stats_plot_paths
    output:
        "plots/sop/" + get_orekit_path_option() + "{constellation}/sgp4-kepler_angles_distances_delays.done"
    shell:
        "touch {output}"

rule satmod_csv:
    input:
        "../../scripts/plots/to_sat_csv.py",
        in_csv="csv/vectors/" + get_orekit_path_option() + "{opp_config}_{coord_frame}_sorted.csv"
    output:
        "csv/vectors/" + get_orekit_path_option() + "satmod/{opp_config}_{coord_frame}_{leo_modname}.csv"
    shell:
        "python3 ../../scripts/plots/to_sat_csv.py {input.in_csv} '{wildcards.leo_modname}' > '{output}'"

def get_satmod_csv_paths(wildcards, same_config=True):
    leo_modnames = wildcards.leo_modnames.split("+")

    # one config (= one mobility) -> one csv per satmod 
    if same_config:
        base_path = "csv/vectors/" + get_orekit_path_option() +"satmod/" + wildcards.opp_config + "_" + wildcards.coord_frame + "_"
        return [(base_path + modname + ".csv") for modname in leo_modnames]

    # tw0 configs (= two mobilities) -> two csvs per satmod 
    else:
        mob1_config_str = get_debug_str() + wildcards.constellation + "-" + wildcards.mobility1
        mob2_config_str = get_debug_str() + wildcards.constellation + "-" + wildcards.mobility2
        mob1_base_path = "csv/vectors/" + get_orekit_path_option() + "satmod/" + mob1_config_str + "_" + wildcards.coord_frame + "_"
        mob2_base_path = "csv/vectors/" + get_orekit_path_option() + "satmod/" + mob2_config_str + "_" + wildcards.coord_frame + "_"
        return [(mob1_base_path + modname + ".csv") for modname in leo_modnames] + [(mob2_base_path + modname + ".csv") for modname in leo_modnames]
    
rule plot_orbs_of_mobility:
    input:
        "../../scripts/plots/plot_orbs.py",
        coord_paths=get_satmod_csv_paths
    output:
        "plots/orbs/mobility/{opp_config}_{coord_frame}_{leo_modnames}.html"
    params:
        csv_paths= lambda wildcards, input: quote_strs(input.coord_paths)
    shell:
        """
        python3 ../../scripts/plots/plot_orbs.py -c {params.csv_paths} -o '{output}' -e
        """

rule plot_orbs_of_mobilities:
    input:
        "../../scripts/plots/plot_orbs.py",
        coord_paths=lambda wildcards: get_satmod_csv_paths(wildcards, False)
    output:
        "plots/orbs/mobilities/" + get_debug_str() + "{constellation}_{coord_frame}_{mobility1}-{mobility2}_{leo_modnames}.html"
    params:
        csv_paths= lambda wildcards, input: quote_strs(input.coord_paths),
    shell:
        "python3 ../../scripts/plots/plot_orbs.py -c {params.csv_paths} -o '{output}' -e"

rule make_symbolic_link_and_hidden_data:
    input:
        "omnetpp.ini"
    output:
        "test.txt"
    #shell:
    #    """
    #    touch {output}
    #    touch old/{output}
    #    """
    run:
        os.makedirs("./old", exist_ok=True)
        if Path("./old/test.txt").exists():
            print("old test txt exists")
        else:
            print("old test txt does not exist")

        #with open(output[0], "w") as output_f:
        #    output_f.write(" ")
        
        with open("old/" + output[0], "w") as output_f_copy:
            output_f_copy.write("huasefuiasdiufhasidfhiasdhfiashui")
        
        os.symlink("old/" + output[0], output[0])